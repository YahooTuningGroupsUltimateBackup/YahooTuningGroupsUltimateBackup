1 'ODEIONPC.BAS   0.04   14/MAY/96	[ODEIONPC.001 = ODEION.015]
2 'Melodic Improvisation Program
3 'Copyright (C) Peter Sault 1992-1996
4 'All rights reserved worldwide
1000 'ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
1010 'ณ M A I N L I N E ณ
1020 'ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
1030 OPTION BASE 0:ON ERROR GOTO 2500
1090 '- - - - - - - - - - - - -
1100 DIM RATIO#(12),RATIO$(12)		'chromatic interval ratios
1110 DIM PITCH!(12)			'chromatic vibration frequencies
1120 DIM SCALE%(53,12),SCALE$(53)	'scales
1130 DIM MOVE%(12,12)			'perfect interval map (one scale)
1140 DIM SCORE!(511)			'recording of melody
1190 '- - - - - - - - - - - - -
1200 GOSUB 16000			'init constants
1210 RESTORE 61000:FOR IA%=0 TO 12:READ F1#,F2#,RATIO$(IA%):RATIO#(IA%)=F2#/F1#:NEXT IA%
1220 RESTORE 63000:FOR IA%=0 TO 53:FOR IB%=0 TO 12:READ SCALE%(IA%,IB%):NEXT IB%:READ SCALE$(IA%):NEXT IA%
1230 GOSUB 5000				'load move table
1240 GOSUB 2600				'init defaults
1250 CALL LISTINI			'LISTMAN initialization	
1290 '-------------------------
2000 COLOR 7,0:CLS:PRINT PRID$;"   Melodic Composer   Copyright (C) Peter Wakefield Sault 1992-1996":PRINT
2010 PRINT"<1> Improvise and Record Melody"
2020 PRINT"<2> Play Recorded Melody"
2030 PRINT"<3> Exit to DOS"
2040 PRINT:PRINT"Select menu option number ";
2050 WHILE INKEY$>"":WEND:KB$="":WHILE KB$="":KB$=INKEY$:WEND
2060 WORK!=VAL(KB$)
2070 IF WORK!<1! OR WORK!>3! OR WORK!<>FIX(WORK!) THEN BEEP:GOTO 2050
2080 FUN%=CINT(WORK!):IF FUN%=3 GOTO 2400
2090	ON FUN% GOSUB 3000,4000
2100	GOTO 2000
2190 '- - - - - - - - - - - - -
2400 COLOR 7,0:CLS:PRINT PRID$;" ended"
2410 END
2470 'ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
2480 'ณ ERROR HANDLER ณ
2490 'ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
2500 COLOR 7,0:CLS:PRINT PRID$;" error";ERR;" line";ERL
2510 RESET:STOP
2560 'ษอออออออออออออออออออออป
2570 'บ INITIALIZE DEFAULTS บ
2580 'ศอออออออออออออออออออออผ
2590 'tonic pitch = 360 Hz
2599 '''FOR IA%=0 TO 12:PITCH!(IA%)=360!*RATIO#(IA%):NEXT IA%
2600 PITCH!(0)=360!
2608 'octave range = 7
2609 '''RANGE%=7:OCTL!=1!/2!^CSNG((RANGE%-1)\2):OCTU!=2!^CSNG(RANGE%\2)
2610 RANGE%=7
2618 'tempo = 240 beats per minute
2620 BPM!=240!
2628 'rhythm = 8 beats per measure
2630 BEAT%=8
2638 'maximum relative interval = tonal class 12 (inversion disabled)
2640 INVI%=12
2648 'successive note duplication = Y
2650 DUPS%=1
2658 'alternate note duplication = Y
2660 DUPA%=1
2668 'perfect alternate relative intervals = N
2670 PERA%=0
2678 'modulation tonal class = 12 (modulation disabled)
2680 MODI%=12
2690 RETURN
2770 'ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
2780 'ณ SERVICE - Numeric Edit ณ
2790 'ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
2800 WORK$=STR$(WORK!):IF WORK!=>0! THEN WORK$=RIGHT$(WORK$,LEN(WORK$)-1)
2810 RETURN
2870 'ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
2880 'ณ SERVICE - Display Default ณ
2890 'ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
2900 LOCATE ,1:PRINT"[";WORK$;"]";TAB(8);:RETURN
2970 'ษอออออออออออออออออออออป
2980 'บ MELODIC COMPOSITION บ
2990 'ศอออออออออออออออออออออผ
3000 COLOR 7,0:CLS
3002 LISU%=53							
3004 LICL%=2:LICR%=61:LIRB%=20:LIRT%=5	'LISTMAN area (base 1)	
3006 LIBT%=2:LFG1%=5:LFG2%=3:LIBG%=1:LISF%=2			
3010 MESC$="cancel":MENT$="select":LISI$="scale"
3016 '----------------------------------
3020 CALL LISTBOX(LIRT%,LIRB%,LICL%,LICR%,LIBT%,LFG1%,LFG2%,LIBG%,RC%) 
3030 CALL LISTMAN(SCALE$(0),LISU%,LISS%,LISF%,LIKB%,RET%)	
3032 CALL LISTCLR(RET%)						
3034 IF LIKB%=27 GOTO 3900		'[Esc] = abandon	
3036 SCALI%=LISS%			'[Enter] = use scale
3038 '----------------------------------
3040 COLOR 7,0:CLS:LOCATE 1,1
3044 FOR IB%=0 TO 12:PRINT MID$("๚",SCALE%(SCALI%,IB%)+1,1);:NEXT IB%
3046 LOCATE ,16:PRINT SCALE$(SCALI%)
3050 '----------------------------------
3060 LOCATE 5,1:PRINT TAB(79);:WORK!=PITCH!(0):GOSUB 2800:GOSUB 2900
3062 INPUT "Middle tonic pitch (60-720 Hz)อออออออออออออ ",WORK$
3064 IF WORK$>"" THEN WORK!=VAL(WORK$)
3065 IF WORK!<60! OR WORK!>720! THEN BEEP:GOTO 3060
3066 FOR IA%=0 TO 12:PITCH!(IA%)=WORK!*RATIO#(IA%):NEXT IA%
3068 LOCATE 5:GOSUB 2800:GOSUB 2900
3069 '
3090 LOCATE 6,1:PRINT TAB(79);:WORK!=CSNG(RANGE%):GOSUB 2800:GOSUB 2900
3092 INPUT "Range (1-7 octaves)ออออออออออออออออออออออออ ",WORK$
3094 IF WORK$>"" THEN WORK!=VAL(WORK$)
3095 IF WORK!<1! OR WORK!>7! THEN BEEP:GOTO 3090
3096 RANGE%=CINT(WORK!):OCTL!=1!/2!^CSNG((RANGE%-1)\2):OCTU!=2!^CSNG(RANGE%\2)
3098 LOCATE 6:WORK!=CSNG(RANGE%):GOSUB 2800:GOSUB 2900
3099 '
3100 LOCATE 7,1:PRINT TAB(79);:WORK!=BPM!:GOSUB 2800:GOSUB 2900
3102 INPUT "Tempo (60-720 beats per minute)ออออออออออออ ",WORK$
3103 IF WORK$>"" THEN WORK!=VAL(WORK$)
3104 IF WORK!<60! OR WORK!>720! THEN BEEP:GOTO 3100
3105 '''BPM!=FIX(WORK!):DUR!=1092.389/BPM!
3106 BPM!=FIX(WORK!)						'[0.04]
3107 DURI%=CINT(60000!/BPM!)	'get beat period (millisecs)	'[0.04]
3108 LOCATE 7:GOSUB 2800:GOSUB 2900
3109 '
3110 LOCATE 8,1:PRINT TAB(79);:WORK!=CSNG(BEAT%):GOSUB 2800:GOSUB 2900
3112 INPUT "Rhythm (1-64 beats per measure)ออออออออออออ ",WORK$
3114 IF WORK$>"" THEN WORK!=VAL(WORK$)
3115 IF WORK!<1! OR WORK!>64! THEN BEEP:GOTO 3110
3116 BEAT%=CINT(WORK!)
3118 LOCATE 8:WORK!=CSNG(BEAT%):GOSUB 2800:GOSUB 2900
3119 '
3120 LOCATE 9,1:PRINT TAB(79);:WORK!=CSNG(INVI%+1):GOSUB 2800:GOSUB 2900
3122 INPUT "Maximum step (chromatic degree, 7-13)ออออออ ",WORK$
3124 IF WORK$>"" THEN WORK!=VAL(WORK$)
3125 IF WORK!<7! OR WORK!>13! THEN BEEP:GOTO 3120
3126 INVI%=CINT(WORK!)-1
3128 LOCATE 9:WORK!=CSNG(INVI%+1):GOSUB 2800:GOSUB 2900
3129 '
3130 LOCATE 10,1:PRINT TAB(79);:WORK$=MID$("NY",DUPS%+1,1):GOSUB 2900
3132 INPUT "Successive note duplication (Y/N)ออออออออออ ",WORK$
3140 IF WORK$>"" THEN IA%=INSTR("YNyn",WORK$) ELSE IA%=2-DUPS%
3142 IF IA%=0 OR LEN(WORK$)>1 THEN BEEP:GOTO 3130
3150 DUPS%=(IA% MOD 2)	'0=N, 1=Y
3152 LOCATE 10:WORK$=MID$("NY",DUPS%+1,1):GOSUB 2900
3158 '
3160 LOCATE 11,1:PRINT TAB(79);:WORK$=MID$("NY",DUPA%+1,1):GOSUB 2900
3162 INPUT "Alternate note duplication (Y/N)อออออออออออ ",WORK$
3164 IF WORK$>"" THEN IA%=INSTR("YNyn",WORK$) ELSE IA%=2-DUPA%
3165 IF IA%=0 OR LEN(WORK$)>1 THEN BEEP:GOTO 3160
3166 DUPA%=(IA% MOD 2)	'0=N, 1=Y
3168 LOCATE 11:WORK$=MID$("NY",DUPA%+1,1):GOSUB 2900
3169 '
3170 LOCATE 12,1:PRINT TAB(79);:WORK$=MID$("NY",PERA%+1,1):GOSUB 2900
3172 INPUT "Perfect alternate intervals (Y/N)ออออออออออ ",WORK$
3174 IF WORK$>"" THEN IA%=INSTR("YNyn",WORK$) ELSE IA%=2-PERA%
3175 IF IA%=0 OR LEN(WORK$)>1 THEN BEEP:GOTO 3170
3176 PERA%=(IA% MOD 2)	'0=N, 1=Y
3178 LOCATE 12:WORK$=MID$("NY",PERA%+1,1):GOSUB 2900
3188 '
3190 LOCATE 13,1:PRINT TAB(79);:WORK!=CSNG(MODI%+1):GOSUB 2800:GOSUB 2900
3192 INPUT "Modulation note (chromatic degree, 2-13)อออ ",WORK$
3194 IF WORK$>"" THEN WORK!=VAL(WORK$)
3195 IF WORK!<2! OR WORK!>13! THEN BEEP:GOTO 3190
3196 IA%=CINT(WORK!)-1:IF SCALE%(SCALI%,IA%)=0 THEN BEEP:GOTO 3190
3198 MODI%=IA%:LOCATE 13:WORK!=CSNG(IA%+1):GOSUB 2800:GOSUB 2900
3258 '
3260 GOSUB 9000				'display interval map
3270 LOCATE 15,1:PRINT"Press [Esc] to stop melodic composition";
3280 '
3290 'ออออออออออออออออออออออ[COMPOSE MELODY]อออออออออออออออออออออป
3300 IA%=0:IB%=0:IS%=0:WRAP%=0:OCTA!=1!				'บ
3310 BEAN%=4:MODR!=1!						'บ
3320 RANDOMIZE TIMER						'บ
3334 CALL TIMINIT						'[0.04]
3390 'Loop until [Esc] pressed					'บ
3400 WHILE INKEY$<>CHR$(27)					'บ
3410	'Save vibration number for playback (IS=note pointer)	'บ
3420	SCORE!(IS%)=OCTA!*MODR!*PITCH!(IA%)			'บ
3430	'Play note						'บ
3439	'''SOUND SCORE!(IS%),DUR!				'บ
3440	FREQ%=CINT(SCORE!(IS%))					'[0.04]
3442	CALL SPEAKON(FREQ%)					'[0.04]
3444	CALL TIMWAIT(DURI%)					'[0.04]
3446	CALL SPEAKOFF						'[0.04]
3450	'Bump recording note pointer				'บ
3460	IS%=((IS%+1) MOD 512)					'บ
3470	'Set flag if recording wrap has occurred		'บ
3500	IF IS%=0 THEN WRAP%=1					'บ
3510	'Save pitch class for comparison during			'บ
3520	'selection of next note to be played.			'บ
3530	IC%=IB%:IB%=IA%						'บ
3558	'Select next note to be played.				'บ
3560	'Repeat if same note as last one is selected		'บ
3570	'or pitch class is not in current scale type.		'บ
3600	IA%=INT(RND*12)		'returns 0 thru 11		'บ
3610	IF MOVE%(IB%,IA%)=0 OR SCALE%(SCALI%,IA%)=0 GOTO 3600	'บ
3620	IF (DUPS%=0 AND IA%=IB%) OR (DUPA%=0 AND IA%=IC%) GOTO 3600
3630	IF PERA%=1 AND MOVE%(IC%,IA%)=0 GOTO 3600 'perf alts too บ
3650	'Invert if relative interval greater than maximum	'บ
3660	IF IA%-IB%>INVI% AND OCTA!>OCTL! THEN OCTA!=OCTA!/2!	'บ
3670	IF IB%-IA%>INVI% AND OCTA!<OCTU! THEN OCTA!=OCTA!*2!	'บ
3678	'Modulate if specified chromatic degree played on beat 1'บ
3680	BEAN%=(BEAN% MOD BEAT%)+1				'บ
3682	IF MODI%=IA% AND BEAN%=1 THEN MODR!=MODR!*RATIO#(MODI%):IA%=0:IB%=IB%-MODI%
3684	WHILE IB%<0:IB%=IB%+12:WEND				'บ
3686	WHILE MODR!=>2!:MODR!=MODR!/2!:WEND			'บ
3800 WEND							'บ
3810 CALL TIMEXIT						'[0.04]
3890 'อออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผ
3900 RETURN
3970 'ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
3980 'ณ MELODY PLAYBACK ณ
3990 'ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
4000 COLOR 7,0:CLS
4010 PRINT"Press [Esc] to cancel melody playback"
4020 IF WRAP%=0 THEN IT%=0:IU%=IS%+1 ELSE IT%=IS%:IU%=512
4022 CALL TIMINIT						'[0.04]
4030 WHILE INKEY$<>CHR$(27)
4039	'''SOUND SCORE!(IT%),DUR!
4040	FREQ%=CINT(SCORE!(IT%))					'[0.04]
4042	CALL SPEAKON(FREQ%)					'[0.04]
4044	CALL TIMWAIT(DURI%)					'[0.04]
4046	CALL SPEAKOFF						'[0.04]
4050	IT%=((IT%+1) MOD IU%)
4060 WEND
4062 CALL TIMEXIT						'[0.04]
4070 RETURN
4970 'ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
4980 'ณ LOAD MOVE TABLE ณ
4990 'ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
5000 FOR IA%=0 TO 12:FOR IB%=0 TO 12:MOVE%(IA%,IB%)=0:NEXT IB%,IA%
5010 FOR IA%=0 TO 12:FOR IB%=0 TO 12
5020	WA#=RATIO#(IA%)*RATIO#(IB%)
5030	WHILE WA#=>2#:WA#=WA#/2#:WEND
5040	IC%=((IA%+IB%) MOD 12)
5050	IF CSNG(WA#)=CSNG(RATIO#(IC%)) THEN MOVE%(IA%,IC%)=1:MOVE%(IC%,IA%)=1
5060 NEXT IB%,IA%
5070 RETURN
8970 'ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
8980 'ณ INTERVAL COUNTER ณ
8990 'ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
9000 ICNT%=0:GOSUB 9400			'dsply int chart box
9010 FOR IP%=0 TO 11			'for each initial note
9020	IF SCALE%(SCALI%,IP%)=0 GOTO 9070
9030	  FOR IQ%=IP%+1 TO 12		'for each interval
9040	    IF MOVE%(IP%,IQ%)=1 AND SCALE%(SCALI%,IQ%)=1 THEN ICNT%=ICNT%+1
9050	  NEXT IQ%
9060	  GOSUB 9600			'dsply int chart column
9070 NEXT IP%
9080 LOCATE SR%+16,SC%:PRINT USING "###";ICNT%;:PRINT"perfect relative intervals";
9090 RETURN
9370 'ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
9380 'ณ DISPLAY INTERVAL CHART BOX ณ
9390 'ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
9400 SR%=3:SC%=51:SD%=SC%+2
9402 LOCATE SR%,SC%+5	:PRINT"n = chromatic degree";
9410 LOCATE SR%+1,SC%	:PRINT"ษ";STRING$(28,"อ");"ป";
9420 LOCATE SR%+15,SC%	:PRINT"ศ";STRING$(28,"อ");"ผ";
9430 FOR IA%=0 TO 12
9440	LOCATE SR%+IA%+2,SC%:PRINT"บ ";
9450	IF SCALE%(SCALI%,IA%)=0 THEN PRINT" ๙"; ELSE PRINT USING "##";IA%+1;
9460	PRINT TAB(80);"บ";
9470 NEXT IA%
9490 RETURN
9570 'ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
9580 'ณ DISPLAY INTERVAL CHART COLUMN ณ
9590 'ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
9600 X1%=-1:X2%=-1
9610 FOR IA%=0 TO 12
9620	IF MOVE%(IP%,IA%)=1 OR IA%=IP% THEN X2%=IA%
9630	IF X1%<0 THEN X1%=X2%
9640 NEXT IA%
9650 IF X1%=X2% GOTO 9820
9660 '
9670 SD%=SD%+2
9680 FOR IA%=0 TO 12
9690	LOCATE SR%+IA%+2,SD%
9700	IF IA%=IP% THEN PRINT"อ";:GOTO 9740
9710	IF MOVE%(IP%,IA%)=1 AND SCALE%(SCALI%,IA%)=1 THEN PRINT CHR$(17);:GOTO 9770
9720	IF IA%>X1% AND IA%<X2% THEN PRINT" ณ";
9730	GOTO 9800
9740	IF IA%=X1% THEN PRINT"ธ";:GOTO 9800
9750	IF IA%=X2% THEN PRINT"พ";:GOTO 9800
9760	PRINT"ต";:GOTO 9800
9770	IF IA%=X1% THEN PRINT"ฟ";:GOTO 9800
9780	IF IA%=X2% THEN PRINT"ู";:GOTO 9800
9790	PRINT"ด";
9800 NEXT IA%
9810 '
9820 RETURN
15960 'ษออออออออออออออออออออออป
15970 'บ INITIALIZE CONSTANTS บ
15980 'ศออออออออออออออออออออออผ
16000 PRID$="ODEIONPC 0.04"
16030 KEY OFF:FOR Z%=1 TO 10:KEY Z%,"":NEXT Z%	'nullify function keys
16040 KBUF!=64!				'k/b buf seg
16050 '---[ASCII k/b codes]---
16060 KB1$=CHR$(8)+CHR$(13)+CHR$(27)	'backsp/CR/Esc
16070 '---[extended ASCII k/b codes (2nd char)]---
16090 KB3$="GKMOS"			'Home/csrL/csrR/End/Del
16100 KB5$=";<=>?@ABCD"			'F1 thru F10
16102 KB7$="IQHP"			'PgUp/PgDn/csrU/csrD	LISTMAN
16104 KB8$=CHR$(27)+CHR$(13)		'Esc/Enter		LISTMAN
16110 KB9$=";<=>?@ABCDTU"		'F1 thru F12 (F11=[Shift-F1], F12=[Shift-F2])
16120 INS$=CHR$(0)+"R":INS%=0		'Ins kb_char and flag
16132 '
16134 '---[ascertain monitor type (0=Mono 1=CGA)]---
16140 DEF SEG=0:IF (PEEK(&H410) AND &H30)=&H30 THEN MON%=0 ELSE MON%=1
16150 DEF SEG:IF MON%=1 GOTO 16220
16152 '- - - - - - - - - - - - -
16154 'attributes for MONO (=default)
16160 '''CTF%=0:CTB%=7
16170 CBF%=7:CBB%=0
16180 '''CPF%=7:CPB%=0
16190 CCF%=7:CCB%=0
16200 GOTO 16300
16210 '- - - - - - - - - - - - -
16212 'attributes/colours for CGA
16220 SCREEN 0,1		'enable colour
16230 '''CTF%=1:CTB%=3		'status line (screen row 1)
16240 CBF%=7:CBB%=1		'ground
16250 '''CPF%=3:CPB%=0		'list management (LISTMAN)
16260 CCF%=4:CCB%=0		'PC function keys (screen row 25)
16270 '-------------------------
16300 LEGR%=2			'screen row for control key legends
16310 TEXR%=23			'screen row for msgs/prompts/input etc
16320 STAR%=1			'screen row for status display
16340 '''SONA%=2400		'sound - toggle switch signal
16350 '''SOND%=1			'sound - duration
16360 '''SONE%=64			'sound - keystroke error
16370 '''SONO%=1200		'sound - warning alarm
16380 '''SONM%=1700		'sound - menu signal
16390 RETURN
21970 'ษออออออออออออออออออออออออออออออออออออป
21980 'บ KEYBOARD STRING INPUT (CONTROLLED) บ
21990 'ศออออออออออออออออออออออออออออออออออออผ
22000 LOCATE LEGR%,2
22010 COLOR CBF%+8,CBB%:PRINT CHR$(27);CHR$(26);:COLOR CBF%:PRINT" cursor  ";
22020 COLOR CBF%+8:PRINT CHR$(17);"ฤู";:COLOR CBF%:PRINT" enter  ";
22030 COLOR CBF%+8:PRINT"Esc";:COLOR CBF%:PRINT" cancel";TAB(80);
22040 TESC%=0:TERR%=0:CSRT%=1:TEXI%=1
22050 IF LEN(TEXI$)<TEXL% THEN TEXI$=TEXI$+SPACE$(TEXL%-LEN(TEXI$))
22060 IF LEN(TEXI$)>TEXL% THEN TEXI$=LEFT$(TEXI$,TEXL%)
22070 '
22080 LOCATE TEXR%,2,0:COLOR CBF%+16,CBB%:PRINT CHR$(2);:COLOR CBF%:PRINT" ";TEXP$;
22090 LOCATE TEXR%,4+LEN(TEXP$),0:COLOR CBB%,CBF%:PRINT CHR$(221);TEXI$;CHR$(222);
22100 IF TEXE%=0 GOTO 22140
22110	LOCATE TEXR%,4+LEN(TEXP$)+TEXE%,0:COLOR CBB%+16,CBF%:PRINT MID$(TEXI$,TEXE%,1);
22120	TEXI%=TEXE%:TEXE%=0
22130 '
22140 LOCATE ,4+LEN(TEXP$)+TEXI%,CSRT%:GOSUB 33300
22150 ON LEN(KB$) GOTO 22200,22600
22160 GOTO 22630					'[garbage]
22190 '---[ * backsp * CR * Esc * ]---
22200 ON INSTR(KB1$,KB$) GOTO 22220,22750,22260
22210 GOTO 22400
22220 IF TEXI%<2 GOTO 22630				'[backsp]
22230	TEXI%=TEXI%-1
22240	TEXI$=LEFT$(TEXI$,TEXI%-1)+RIGHT$(TEXI$,TEXL%-TEXI%)+" "
22250	GOTO 22090
22260 TESC%=1:TEXI$="":GOTO 22750			'[Esc]
22390 '---[ * label characters * ]---
22400 IF KB$<" " OR KB$>CHR$(126) GOTO 22630
22410	IF INS%=1 GOTO 22460				'insert mode active
22420	  COLOR CBB%,CBF%:PRINT KB$;			'text char
22430	  MID$(TEXI$,TEXI%,1)=KB$
22440	  IF TEXI%<TEXL% THEN TEXI%=TEXI%+1 ELSE TEXI%=1
22450	  GOTO 22140
22460	IF RIGHT$(TEXI$,1)>" " GOTO 22630		'no room
22470	  TEXI$=LEFT$(TEXI$,TEXI%-1)+KB$+RIGHT$(TEXI$,TEXL%-TEXI%+1)
22480	  TEXI$=LEFT$(TEXI$,TEXL%)
22490	  IF TEXI%<TEXL% THEN TEXI%=TEXI%+1 ELSE TEXI%=1
22500	  GOTO 22090
22590 '---[ * Home * csrL * csrR * End * Del * ]---
22600 KB$=RIGHT$(KB$,1)
22610 IF KB$="2" THEN GOSUB 34000:GOTO 22080		'dsply last msg
22620 ON INSTR(KB3$,KB$) GOTO 22640,22650,22670,22690,22730
22629 '''SOUND SONE%,SOND%:GOTO 22140			'[garbage]
22630 GOTO 22140				'[garbage]	'[0.04]
22640 TEXI%=1:GOTO 22140				'[Home]
22650 IF TEXI%>1 THEN TEXI%=TEXI%-1 ELSE TEXI%=TEXL%	'[csrL]
22660	GOTO 22140
22670 IF TEXI%<TEXL% THEN TEXI%=TEXI%+1 ELSE TEXI%=1	'[csrR]
22680	GOTO 22140
22690 TEXI%=TEXL%					'[End]
22700	WHILE MID$(TEXI$,TEXI%,1)=" " AND TEXI%>1:TEXI%=TEXI%-1:WEND
22710	IF TEXI%<TEXL% THEN TEXI%=TEXI%+1 ELSE TEXI%=1
22720	GOTO 22140
22730 TEXI$=LEFT$(TEXI$,TEXI%-1)+RIGHT$(TEXI$,TEXL%-TEXI%)+" "	'[Del]
22740	GOTO 22090
22748 '* reset [Ins] * wipe ready/msg line * wipe ctrl key list *
22750 CSRT%=0:GOSUB 33600:RETURN				
23370 'ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
23380 'ณ EDIT-2 PARAMETER STRING ณ
23390 'ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
23400 WHILE LEFT$(TEXI$,1)=" ":TEXI$=RIGHT$(TEXI$,LEN(TEXI$)-1):WEND	'strip blanks from...
23410 WHILE RIGHT$(TEXI$,1)=" ":TEXI$=LEFT$(TEXI$,LEN(TEXI$)-1):WEND	'...left and right
23420 RETURN
33250 'ษอออออออออออออออออออออออออออป
33260 'บ    KEYBOARD INPUT WAIT    บ
33270 'บ With lock keys status and บ
33280 'บ continuous clock displays บ
33290 'ศอออออออออออออออออออออออออออผ
33300 CSRR%=CSRLIN:CSRC%=POS(0):LOCATE ,,CSRT%
33310 CALL KEYCLEAR:KB$="":DEF SEG=KBUF!
33320 WHILE KB$=""
33330	KB$=INKEY$:XSH%=PEEK(23)
33340	IF TIME$=TIMH$ GOTO 33370
33350	  TIMH$=TIME$
33360	  COLOR CBF%,CBB%:LOCATE 24,71,0:PRINT TIMH$;
33370	IF YSH%=XSH% GOTO 33440
33380	  YSH%=XSH%
33390	  COLOR CCF%+8,CCB%:LOCATE 25,76,0
33400	  IF (YSH% AND 64)=0 THEN PRINT" "; ELSE PRINT"C";
33410	  IF (YSH% AND 128)=0 THEN PRINT" ";:INS%=0 ELSE PRINT"I";:INS%=1
33420	  IF (YSH% AND 32)=0 THEN PRINT" "; ELSE PRINT"N";
33430	  IF (YSH% AND 16)=0 THEN PRINT" ";:SCR%=0 ELSE PRINT"S";:SCR%=1
33440	LOCATE CSRR%,CSRC%,CSRT%
33450	IF KB$=INS$ THEN KB$=""	'reject [Ins] char (x-asc 0,82)
33460 WEND
33470 DEF SEG
33480 RETURN
33570 'ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
33580 'ณ RESET KEYBOARD INSERT STATE ณ
33590 'ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
33600 DEF SEG=KBUF!:POKE 23,(PEEK(23) AND &H7F):DEF SEG
33610 INS%=0:COLOR CCF%,CCB%:LOCATE 25,77,0:PRINT" ";
33620 RETURN
33960 'ษอออออออออออออออออป
33970 'บ DISPLAY MESSAGE บ
33980 'บ for (DLY%) secs บ
33990 'ศอออออออออออออออออผ
34000 IF DLY%<1 THEN DLY%=3	'default period
34010 IF MSG$="" THEN MSG$="No message"
34020 GOSUB 34300		'dsply msg (no wait)
34030 TIMH$="":LOCATE 24
34040 WHILE DLY%>-1
34050	WHILE TIMH$=TIME$:WEND
34060	TIMH$=TIME$:LOCATE ,71:PRINT TIMH$;
34070	DLY%=DLY%-1
34080 WEND
34100 RETURN
34260 'ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
34270 'ณ DISPLAY MESSAGE ณ
34280 'ณ    (no wait)    ณ
34290 'ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
34300 IF LEN(MSG$)>73 THEN MSG$=LEFT$(MSG$,73)
34310 COLOR CBF%+8,CBB%:LOCATE TEXR%,2,0:PRINT CHR$(2);
34319 '''COLOR CBF%:PRINT" ";MSG$;TAB(80);:SOUND SONO%,SOND%
34320 COLOR CBF%:PRINT" ";MSG$;TAB(80);				'[0.04]
34330 RETURN
60970 '
60980 '------------------------------------------------------------------
60990 '--==๐๐[ INTERVAL RATIOS ]๐๐==--
61000 DATA 1#,1#,	"Unison"
61010 '''DATA 16#,17#,	"Semitone"
61020 DATA 15#,16#,	"Semitone"
61030 '''DATA 9#,10#,	"Minor Step"
61040 DATA 8#,9#,	"Major Step (Tone)"
61050 DATA 5#,6#,	"Minor 3rd"
61060 DATA 4#,5#,	"Major 3rd"
61070 DATA 3#,4#,	"Perfect 4th"
61080 DATA 32#,45#,	"Augmented 4th"
61090 '''DATA 17#,24#,	"Tritone"
61100 '''DATA 12#,17#,	"Tritone"
61110 '''DATA 45#,64#,	"Diminished 5th"
61120 DATA 2#,3#,	"Perfect 5th"
61130 DATA 5#,8#,	"Minor 6th"
61140 DATA 3#,5#,	"Major 6th"
61150 '''DATA 4#,7#,	"Harmonic 7th"
61160 DATA 9#,16#,	"Grave 7th"
61170 '''DATA 5#,9#,	"Minor 7th"
61180 DATA 8#,15#,	"Major 7th"
61190 '''DATA 9#,17#,	"Major 7th"
61200 DATA 1#,2#,	"Perfect 8ve"
62970 '------------------------------------------------------------------
62980 '			--==๐๐[ SCALE TABLE ]๐๐==--
62990 'Table of Scale Types (Chromatic, Diatonic and Harmonic)
63000 DATA 1,1,1,1,1,1,1,1,1,1,1,1,1,"CHROMATIC (HALFTONE)"
63010 DATA 1,1,0,1,1,0,1,0,1,1,0,1,1,"SYMETRIC MODE I"
63011 DATA 1,0,1,1,0,1,0,1,1,0,1,1,1,"SYMETRIC MODE II"
63012 DATA 1,1,0,1,0,1,1,0,1,1,1,0,1,"SYMETRIC MODE III"
63013 DATA 1,0,1,0,1,1,0,1,1,1,0,1,1,"SYMETRIC MODE IV"
63014 DATA 1,0,1,1,0,1,1,1,0,1,1,0,1,"SYMETRIC MODE V"
63015 DATA 1,1,0,1,1,1,0,1,1,0,1,0,1,"SYMETRIC MODE VI"
63016 DATA 1,0,1,1,1,0,1,1,0,1,0,1,1,"SYMETRIC MODE VII"
63017 DATA 1,1,1,0,1,1,0,1,0,1,1,0,1,"SYMETRIC MODE VIII"
63020 DATA 1,0,1,0,1,0,1,0,1,0,1,0,1,"WHOLE TONE"
63030 DATA 1,0,1,1,0,1,1,0,1,1,0,1,1,"DIMINISHED MODE I"
63032 DATA 1,1,0,1,1,0,1,1,0,1,1,0,1,"DIMINISHED MODE II"
63040 DATA 1,0,0,1,0,0,1,0,0,1,0,0,1,"DIATONIC MINOR 3RDS (CHROMATIC 4THS)"
63050 DATA 1,0,0,0,1,0,0,0,1,0,0,0,1,"DIATONIC MAJOR 3RDS (CHROMATIC 5THS)"
63100 DATA 1,0,1,0,0,1,0,1,0,0,1,0,1,"DUPLEX DIATESSARON MODE I"
63110 DATA 1,0,0,1,0,1,0,0,1,0,1,0,1,"DUPLEX DIATESSARON MODE II"
63120 DATA 1,0,1,0,0,1,0,1,0,1,0,0,1,"DUPLEX DIATESSARON MODE III"
63130 DATA 1,0,0,1,0,1,0,1,0,0,1,0,1,"DUPLEX DIATESSARON MODE IV (PENTATONIC MINOR)"
63140 DATA 1,0,1,0,1,0,0,1,0,1,0,0,1,"DUPLEX DIATESSARON MODE V  (PENTATONIC MAJOR)"
63200 DATA 1,0,1,0,1,0,1,1,0,1,0,1,1,"MULTIPLEX DIAPENTE MODE I   (LYDIAN)"
63210 DATA 1,0,1,0,1,1,0,1,0,1,1,0,1,"MULTIPLEX DIAPENTE MODE II  (MIXOLYDIAN)"
63220 DATA 1,0,1,1,0,1,0,1,1,0,1,0,1,"MULTIPLEX DIAPENTE MODE III (AEOLIAN, MINOR)"
63230 DATA 1,1,0,1,0,1,1,0,1,0,1,0,1,"MULTIPLEX DIAPENTE MODE IV  (LOCRIAN)"
63240 DATA 1,0,1,0,1,1,0,1,0,1,0,1,1,"MULTIPLEX DIAPENTE MODE V   (IONIAN, MAJOR)"
63250 DATA 1,0,1,1,0,1,0,1,0,1,1,0,1,"MULTIPLEX DIAPENTE MODE VI  (DORIAN, CHURCH)"
63260 DATA 1,1,0,1,0,1,0,1,1,0,1,0,1,"MULTIPLEX DIAPENTE MODE VII (PHRYGIAN)"
63300 DATA 1,0,1,1,0,1,0,1,0,1,0,1,1,"MELODIC MODE I   (JAZZ)"
63310 DATA 1,1,0,1,0,1,0,1,0,1,1,0,1,"MELODIC MODE II"
63320 DATA 1,0,1,0,1,0,1,0,1,1,0,1,1,"MELODIC MODE III"
63330 DATA 1,0,1,0,1,0,1,1,0,1,1,0,1,"MELODIC MODE IV"
63340 DATA 1,0,1,0,1,1,0,1,1,0,1,0,1,"MELODIC MODE V"
63350 DATA 1,0,1,1,0,1,1,0,1,0,1,0,1,"MELODIC MODE VI"
63360 DATA 1,1,0,1,1,0,1,0,1,0,1,0,1,"MELODIC MODE VII (DIM. WHOLE TONE, SUPER-LOCRIAN)"
63400 DATA 1,0,1,1,0,1,0,1,1,0,0,1,1,"HARMONIC MODE I"
63410 DATA 1,1,0,1,0,1,1,0,0,1,1,0,1,"HARMONIC MODE II"
63420 DATA 1,0,1,0,1,1,0,0,1,1,0,1,1,"HARMONIC MODE III"
63430 DATA 1,0,1,1,0,0,1,1,0,1,1,0,1,"HARMONIC MODE IV"
63440 DATA 1,1,0,0,1,1,0,1,1,0,1,0,1,"HARMONIC MODE V   (PHRYGIAN DOMINANT)"
63450 DATA 1,0,0,1,1,0,1,1,0,1,0,1,1,"HARMONIC MODE VI"
63460 DATA 1,1,0,1,1,0,1,0,1,1,0,0,1,"HARMONIC MODE VII"
63500 DATA 1,1,0,0,1,1,0,1,0,1,1,0,1,"INVERSE HARMONIC MODE I"
63510 DATA 1,0,0,1,1,0,1,0,1,1,0,1,1,"INVERSE HARMONIC MODE II"
63520 DATA 1,1,0,1,0,1,1,0,1,1,0,0,1,"INVERSE HARMONIC MODE III"
63530 DATA 1,0,1,0,1,1,0,1,1,0,0,1,1,"INVERSE HARMONIC MODE IV"
63540 DATA 1,0,1,1,0,1,1,0,0,1,1,0,1,"INVERSE HARMONIC MODE V"
63550 DATA 1,1,0,1,1,0,0,1,1,0,1,0,1,"INVERSE HARMONIC MODE VI"
63560 DATA 1,0,1,1,0,0,1,1,0,1,0,1,1,"INVERSE HARMONIC MODE VII"
63600 DATA 1,0,1,1,0,0,1,1,1,0,0,1,1,"RHAPSODIC MODE I   (HUNGARIAN)"
63610 DATA 1,1,0,0,1,1,1,0,0,1,1,0,1,"RHAPSODIC MODE II"
63620 DATA 1,0,0,1,1,1,0,0,1,1,0,1,1,"RHAPSODIC MODE III"
63630 DATA 1,1,1,0,0,1,1,0,1,1,0,0,1,"RHAPSODIC MODE IV"
63640 DATA 1,1,0,0,1,1,0,1,1,0,0,1,1,"RHAPSODIC MODE V"
63650 DATA 1,0,0,1,1,0,1,1,0,0,1,1,1,"RHAPSODIC MODE VI"
63660 DATA 1,1,0,1,1,0,0,1,1,1,0,0,1,"RHAPSODIC MODE VII"
